policy instance transient_reliable_qos: qos {
    setting history     = KeepLast;
    setting depth       = 1;
    setting reliability = Reliable;
    setting durability  = TransientLocal;
}

policy instance map_server_topic_qos: qos {
    setting history     = KeepLast;
    setting depth  		= 1;
    setting reliability = Reliable;
    setting durability  = TransientLocal;
}

policy instance costmap_server_info_qos: qos {
    setting history     = KeepLast;
    setting depth  		= 1;
    setting reliability = Reliable;
    setting durability  = TransientLocal;
}

policy instance speed_limit_qos: qos {
	setting depth = 10;
}

policy instance sensor_data_profile: qos {
    setting history     = KeepLast;
    setting depth  		= 5;
    setting reliability = BestEffort;
    setting durability  = Volatile;
    setting deadline = 0;
    setting lifespan = 0;
    setting liveliness = Automatic;
    setting duration = 0;
}

type alias AfterHumbleVersion: OrderedEnum[Ardent, Bouncy, Crystal, Dashing, Eloquent, Foxy, Galactic, Humble, Iron, Jazzy, Kilted] where {_ >= Humble};
type alias BeforeJazzyVersion: OrderedEnum[Ardent, Bouncy, Crystal, Dashing, Eloquent, Foxy, Galactic, Humble, Iron, Jazzy, Kilted] where {_ <= Jazzy};

type alias Natural: float32 where {_ >= 0};
type alias Nanosecond: Natural;
type alias Second: Natural;
type alias Radian: float32;
type alias Meter32: float32;
type alias Meter: float;

message alias RestrictedLaserScan: sensor_msgs/LaserScan {
    field header: std_msgs/Header;
	field angle_min: Radian;
    field angle_max: Radian;
    field angle_increment: Radian;
    field time_increment: Second;
    field scan_time: Natural;
    field range_min: Meter32;
    field range_max: Meter32;
    field ranges: float32[];
    field intensities: float32[];
} where {
	angle_min < angle_max;
    range_min < range_max;
}

message alias RestrictedLoadMap: nav2_msgs/LoadMap {
	request field map_url: string;
	response field map: nav_msgs/OccupancyGrid;
    response field result: uint8 where {(_ >= 0 and _ <= 3) or _ == 255};
}

node type amcl_type {
    context distribution: AfterHumbleVersion;

    param robot_model_type: Enum[DifferentialMotionModel, OmniMotionModel];
	param scan_topic: string;
    param map_topic: string;

	optional param alpha1: double = 0.2;
	optional param alpha2: double = 0.2;
	optional param alpha3: double = 0.2;
	optional param alpha4: double = 0.2;
	optional param alpha5: double = 0.2;
    optional param base_frame_id: string = "base_footprint";
    optional param beam_skip_distance: Natural = 0.5;
    optional param beam_skip_error_threshold: double = 0.9;
    optional param beam_skip_threshold: double = 0.3;
    optional param do_beamskip: bool = false;
    optional param global_frame_id: string = "map";
    optional param lambda_short: double = 0.1;
    optional param laser_likelihood_max_dist: double = 2.0;
    optional param laser_max_range: double where {_ >= 0 or _ == -1} = 100.0;
    optional param laser_min_range: double where {_ >= 0.0 or _ == -1.0} = -1.0;
	optional param laser_model_type: Enum[Beam, LikelihoodField, LikelihoodFieldProb] = LikelihoodField;
    optional param set_initial_pose: bool = false;
    optional param initial_pose: geometry_msgs/Pose2D = geometry_msgs/Pose2D {
    	x = 0.0,
        y = 0.0,
        z = 0.0,
        yaw = 0.0,
    };
	optional param max_beams: int = 60;
	optional param max_particles: int = 2000;
    optional param min_particles: int = 500;
    optional param odom_frame_id: string = "odom";
    optional param pf_err: double = 0.05;
    optional param pf_z: double = 0.99;
    optional param recovery_alpha_fast: double = 0.0;
    optional param recovery_alpha_slow: double = 0.0;
    optional param resample_interval: Natural = 1;
	optional param save_pose_rate: Natural = 0.5;
    optional param sigma_hit: double = 0.2;
    optional param tf_broadcast: bool = true;
    optional param transform_tolerance: double = 1.0;
    optional param update_min_a: double = 0.2;
    optional param update_min_d: double = 0.25;
    optional param z_hit: double = 0.5;
    optional param z_max: double = 0.05;
    optional param z_rand: double = 0.5;
    optional param z_short: double = 0.005;
    optional param always_reset_initial_pose: bool = false;
    optional param first_map_only: string = false;
    optional param bond_heartbeat_period: double = 0.1;

    @qos{sensor_data_profile}
    subscribes to content(scan_topic): RestrictedLaserScan;

    @qos{sensor_data_profile}
    publishes to particle_cloud: nav2_msgs/ParticleCloud;

    @qos{transient_reliable_qos}
    publishes to amcl_pose: geometry_msgs/PoseWithCovarianceStamped;

    @qos{transient_reliable_qos}
    subscribes to initialpose: geometry_msgs/PoseWithCovarianceStamped where {count(publishers(_)) == 1};

    @qos{transient_reliable_qos}
    subscribes to content(map_topic): nav_msgs/OccupancyGrid;

    provides service reinitialize_global_localization: std_srvs/Empty;
    provides service request_nomotion_srv: std_srvs/Empty;

    broadcast map to odom;
    broadcast odom to base_link;
    broadcast base_link to scan;
} where {
	laser_min_range <= laser_max_range;
    min_particles <= max_particles;
    laser_model_type == Beam -> z_hit + z_max + z_rand + z_short == 1;
    laser_model_type == LikelihoodField -> z_hit + z_rand == 1;
	always_reset_initial_pose -> exists(initial_pose) and set_initial_pose;
    set_initial_pose -> exists(initial_pose);
}

node type controller_server_kilted {
	context distribution: BeforeJazzyVersion;

    param odom_topic: string;

    optional param speed_limit_topic: string ="speed_limit";
	optional param controller_frequency: Natural = 20.0;
    optional param use_realtime_priority: bool = false;
    optional param publish_zero_velocity: bool = false;
    optional param action_server_result_timeout: Second = 10.0;
    optional param costmap_update_timeout: Second = 0.30;
    optional param min_x_velocity_threshold: Natural = 0.0001;
    optional param min_y_velocity_threshold: Natural = 0.0001;
    optional param min_theta_velocity_threshold: Natural = 0.0001;
    optional param failure_tolerance: double where {_ == -1.0 or _ >= 0} = 0.0;
    #optional param enable_stamped_cmd_eval: bool = true; # THIS IS NO LONGER IN THE SOURCE CODE?

    optional param goal_checker_plugins: Plugin[] = [default(SimpleGoalChecker)];
    optional param controller_plugins: Plugin[] = [default(FollowPath)];
    optional param progress_checker_plugins: Plugin[] = [default(SimpleGoalChecker)];

    @qos{transient_reliable_qos}
    subscribes to odom: nav2_msgs/Odometry;

    #@qos{param depth = 1;} # TODO: add this to a depth1 qos
    publishes to cmd_vel: geometry_msgs/TwistStamped;

    @qos{speed_limit_qos}
    subscribes to content(speed_limit_topic): nav2_msgs/SpeedLimit;

    provides action follow_path: nav2_msgs/FollowPath;
} where {
	min_y_velocity_threshold <= min_x_velocity_threshold;
}

plugin type FollowPath {
    optional param plugin: string = "dwb_core::DWBLocalPlanner";
	optional param critics: Plugin[] = [];
    optional param default_critic_namespaces: string[] = [];
    optional param prune_plan: bool = true;
    optional param prune_distance: double = 2.0;
    optional param forward_prune_distance: double = 2.0;
    optional param debug_trajectory_details: bool = false;
    optional param trajectory_generator_name: Plugin = default(StandardTrajectoryGenerator);
    optional param transform_tolerance: double = 0.1;
    optional param shorten_transformed_plan: bool = true;
    optional param short_circuit_trajectory_evaluation: bool = true;
}

type alias RadianSecond: float;

# Critics
plugin type RotateToGoal {
	optional param xy_goal_tolerance: Meter32 = 0.25;
    optional param trans_stopped_velocity: RadianSecond = 0.25;
    optional param slowing_factor: double = -1;
    optional param scale: double = 1.0;
}

plugin type Oscillation {
	optional param oscillation_reset_dist: Meter32 = 0.05;
    optional param oscillation_reset_angle: Radian = 0.2;
    optional param oscillation_reset_time: Second = -1;
    optional param x_only_threshold: double = 0.05;
}

plugin type ObstacleFootprint {
	optional param sum_scores: bool = false;
    optional param scale: double = 1.0;
}

plugin type GoalAlign {
	optional param forward_point_distance: Meter = 0.325;
    optional param aggregation_type: Enum[Last, Sum, Product] = Last;
    optional param scale: double = 1.0;
}

plugin type PathAlign {
	optional param forward_point_distance: Meter = 0.325;
    optional param aggregation_type: Enum[Last, Sum, Product] = Last;
    optional param scale: double = 1.0;
}

plugin type PathDist {
	optional param aggregation_type: Enum[Last, Sum, Product] = Last;
    optional param scale: double = 1.0;
}

plugin type GoalDist {
	optional param aggregation_type: Enum[Last, Sum, Product] = Last;
    optional param scale: double = 1.0;
}

plugin type StandardTrajectoryGenerator {
	optional param sim_period: double = 0.0;
}

plugin type SimpleGoalChecker {
	optional param plugin: string = "nav2_controller::SimpleGoalChecker";
	optional param xy_goal_tolerance: double = 0.25;
    optional param yaw_goal_tolerance: double = 0.25;
    optional param stateful: bool = true;
}

plugin type SimpleProgressChecker {
	optional param plugin: string = "nav2_controller::SimpleProgressChecker";
	optional param required_movement_radius: double = 0.5;
    optional param movement_time_allowance: double = 10.0;
}

node type planner_server {
	optional param planner_plugins: Plugin[] = [default(GridBased)];
    # param bond_hearbeat_period: Natural; # Not in the code

    optional param action_server_result_timeout: Second = 10.0;
    optional param expected_planner_frequency: Natural = 0.0;
    optional param costmap_update_timeout: Second = 0.0;

    #@qos{param depth = 1;} # TODO: same here change this for the depth = 1 qos
    publishes to plan: nav_msgs/Path;

    provides action compute_path_to_pose: nav2_msgs/ComputePathToPose;
    consumes action compute_path_through_poses: nav2_msgs/ComputePathThroughPoses;
}

node type map_server_type {
	param yaml_filename: string;
    optional param topic_name: string = "map";
    optional param frame_id: string = "map";
    optional param filter_info_topic: string = "/costmap_filter_info";

    @qos{map_server_topic_qos}
    publishes to content(topic_name): nav_msgs/OccupancyGrid;

    @qos{transient_reliable_qos}
    publishes to content(filter_info_topic): nav2_msgs/CostmapFilterInfo;

    @qos{transient_reliable_qos}
    publishes to initialpose: geometry_msgs/PoseWithCovarianceStamped;

    provides service map_server/map: nav_msgs/GetMap;
    provides service map_server/load_map: RestrictedLoadMap;
}

node type costmap_type {
	optional param always_send_full_costmap: bool = false;
    optional param map_vis_z: double = 0.0;
    optional param footprint_padding: float = 0.01;
    optional param footprint: string[] = [];
    optional param global_frame: string = map;
    optional param height: Natural = 5;
    optional param width: Natural = 5;
    optional param lethal_cost_threshold: Natural = 100;
    optional param observation_sources: string = "";
    optional param origin_x: double = 0.0;
    optional param origin_y: double = 0.0;
    optional param plugins: Plugin[] = [StaticLayer, ObstacleLayer, InflationLayer];
    optional param filters: Plugin[] = [];
    optional param publish_frequency: Natural = 1.0;
    optional param resolution: Natural = 0.1;
    optional param robot_base_frame: string = base_link;
    optional param robot_radius: double = 0.1;
    optional param rolling_window: bool = false;
    optional param track_unknown_space: bool = false;
    optional param transform_tolerance: double = 0.3;
    optional param initial_transform_timeout: Natural = 60.0;
    optional param trinary_costmap: bool = true;
    optional param unknown_cost_value: double = 255;
    optional param update_frequency: double = 5.0;
    optional param use_maximum: bool = false;

    @qos{transient_reliable_qos}
    subscribes to footprint: geometry_msgs/PolygonStamped where {count(publishers(_)) > 0};

    @qos{transient_reliable_qos}
    publishes to published_footprint: geometry_msgs/PolygonStamped;

    provides service get_cost_costmap: nav2_msgs/GetCostmap;

    broadcast global_frame to robot_base_frame;
}

node type local_costmap_type {
    optional param update_frequency: Natural = 5.0;
    optional param publish_frequency: Natural = 2.0;
    optional param global_frame: string = "odom";
    optional param robot_base_frame: string = "base_footprint";
    optional param use_sim_time: bool = true;
    optional param rolling_window: bool = true;
    optional param width: Natural = 3.0;
    optional param height: Natural = 3.0;
    optional param resolution: Natural = 0.05;
    optional param robot_radius: Natural = 0.4;
    optional param footprint: string = "";
    optional param plugins: Plugin[] = [default(VoxelLayer), default(InflationLayer)];
    optional param filters: Plugin[] = [default(KeepOutFilter)];
    optional param always_send_full_costmap: bool = true;

    @qos{sensor_data_profile}
    subscribes to /scan: sensor_msgs/LaserScan;

    @qos{speed_limit_qos}
    publishes to speed_limit: nav2_msgs/SpeedLimit;

    @qos{transient_reliable_qos}
    subscribes to /costmap_filter_info: nav2_msgs/CostmapFilterInfo;
}

plugin type DenoiseLayer {
    optional param enabled: bool = true;
    optional param minimal_group_size: int = 2;
    optional param group_connectivity_type: int where {_ == 4 or _ == 8} = 8;
}

plugin type InflationLayer {
    optional param plugin: string = "nav2_costmap_2d::InflationLayer";
    optional param cost_scaling_factor: Natural = 3.0;
    optional param inflation_radius: Natural = 0.55;
}

plugin type ObstacleLayer {
	optional param enabled: bool = true;
    optional param footprint_clearing_enabled: bool = true;
    optional param min_obstacle_height: double = 0.0;
    optional param max_obstacle_height: double = 2.0;
    optional param combination_method: Enum[Overwrite, Max, MaxWithoutUnknownOverwrite] = Max;
    optional param observation_sources: Plugin[] = [];

    optional param track_unknown_space: bool = false;
    optional param transform_tolerance: Natural = 0.0;
} where {
	min_obstacle_height < max_obstacle_height;
}

plugin type VoxelLayer {
    optional param plugin: string = "nav2_costmap_2d::VoxelLayer";
    optional param enabled: bool = true;
    optional param publish_voxel_map: bool = true;
    optional param origin_z: double = 0.0;
    optional param z_resolution: double = 0.05;
    optional param z_voxels: int = 16;
    optional param max_obstacle_height: Natural = 2.0;
    optional param mark_threshold: int = 0;
    optional param unknown_threshold: int = 15;
    optional param combination_method: Enum[Overwrite, Max, MaxWithoutUnknownOverwrite] = Max;
	optional param footprint_clearing_enabled: bool = true;

    @qos{transient_reliable_qos}
    publishes to voxel_grid: nav2_msgs/VoxelGrid;

    @qos{transient_reliable_qos}
    publishes to clearing_endpoints: sensor_msgs/PointCloud2;
}

plugin type KeepOutFilter {
   optional param plugin: string = "nav2_costmap_2d::KeepoutFilter";
   optional param filter_info_topic: string = "/costmap_filter_info";
   optional param enabled: bool = true;

    @qos{costmap_server_info_qos}
    subscribes to content(filter_info_topic): nav2_msgs/CostmapFilterInfo;
}

plugin type SpeedFilter {
	optional param speed_limit_topic: string = "speed_limit";
    optional param filter_info_topic: string = "/costmap_filter_info";

    @qos{costmap_server_info_qos}
    subscribes to content(filter_info_topic): nav2_msgs/CostmapFilterInfo;

    @qos{speed_limit_qos}
    subscribes to content(speed_limit_topic): nav2_msgs/SpeedLimit;
}

# Defined since we used Gazebo and it publishes to the needed topics
node type gazebo_type {

    @qos{sensor_data_profile}
    publishes to /scan: sensor_msgs/LaserScan;

    @qos{transient_reliable_qos}
    publishes to odom: nav_msgs/Odometry;
}

system {
    node instance amcl: amcl_type {
        context distribution = Humble;

        param robot_model_type = DifferentialMotionModel;
        param scan_topic = "/scan";
        param map_topic = "map";
        param max_particles = 8000;
        param min_particles = 200;
    }

    node instance controller_server: controller_server_kilted {
        context distribution = Humble;

        param odom_topic = odom;

        param controller_frequency = 5.0;
        param min_theta_velocity_threshold = 0.001;
        param failure_tolerance = 0.3;

        param progress_checker_plugins = [default(SimpleProgressChecker)];
        param goal_checker_plugins = [default(SimpleGoalChecker)];
        param controller_plugins = [follow_path_checker];
    }

    plugin instance follow_path_checker: FollowPath {
        param critics = [rotate_goal, default_Oscillation, default_ObstacleFootprint, goal_align, path_align, path_dist, goal_dist];
        param debug_trajectory_details = true;
        param transform_tolerance = 0.2;
    }

    plugin instance rotate_goal: RotateToGoal {
        param scale = 32;
    }

    plugin instance goal_align: GoalAlign {
        param forward_point_distance = 0.1;
        param scale = 24.0;
    }

    plugin instance path_align: PathAlign {
        param forward_point_distance = 0.1;
        param scale = 32.0;
    }

    plugin instance path_dist: PathDist {
        param scale = 32.0;
    }

    plugin instance goal_dist: GoalDist {
        param scale = 24.0;
    }

    node instance map_server: map_server_type {
        param yaml_filename = "map_server/maps/map.yaml";
    }

    node instance local_costmap: local_costmap_type {
        param plugins = [voxel_layer, default(InflationLayer)];
        param filters = [default(KeepOutFilter)];
    }

    node instance gazebo: gazebo_type { }
}
